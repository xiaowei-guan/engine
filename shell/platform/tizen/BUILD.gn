# Copyright 2020 Samsung Electronics Co., Ltd. All rights reserved.
# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//flutter/shell/platform/tizen/config.gni")

group("tizen") {
  deps = [ ":flutter_tizen_library" ]
}

shared_library("flutter_tizen_library") {
  output_name = "flutter_tizen"

  ldflags = [ "-Wl,-rpath,\$ORIGIN" ]

  deps = [ ":flutter_tizen" ]

  public_configs = [ "//flutter:config" ]
}

source_set("flutter_tizen_headers") {
  public = [
    "public/flutter_platform_view.h",
    "public/flutter_texture_registrar.h",
    "public/flutter_tizen.h",
  ]

  public_deps =
      [ "//flutter/shell/platform/common/cpp:common_cpp_library_headers" ]

  configs +=
      [ "//flutter/shell/platform/common/cpp:desktop_library_implementation" ]

  public_configs =
      [ "//flutter/shell/platform/common/cpp:relative_flutter_library_headers" ]
}

source_set("flutter_tizen") {
  sources = [
    "channels/key_event_channel.cc",
    "channels/lifecycle_channel.cc",
    "channels/localization_channel.cc",
    "channels/navigation_channel.cc",
    "channels/platform_channel.cc",
    "channels/platform_view_channel.cc",
    "channels/settings_channel.cc",
    "channels/text_input_channel.cc",
    "external_texture_gl.cc",
    "flutter_tizen.cc",
    "key_event_handler.cc",
    "tizen_embedder_engine.cc",
    "tizen_event_loop.cc",
    "tizen_renderer.cc",
    "tizen_vsync_waiter.cc",
    "touch_event_handler.cc",
  ]

  configs +=
      [ "//flutter/shell/platform/common/cpp:desktop_library_implementation" ]

  deps = [
    ":flutter_tizen_headers",
    "//flutter/shell/platform/common/cpp:common_cpp",
    "//flutter/shell/platform/common/cpp:common_cpp_input",
    "//flutter/shell/platform/common/cpp/client_wrapper:client_wrapper",
    "//flutter/shell/platform/embedder:flutter_engine",
    "//third_party/rapidjson",
  ]

  include_dirs = [
    "$custom_sysroot/usr/include",
    "$custom_sysroot/usr/include/appfw",
    "$custom_sysroot/usr/include/base",
    "$custom_sysroot/usr/include/dlog",
    "$custom_sysroot/usr/include/ecore-1",
    "$custom_sysroot/usr/include/ecore-evas-1",
    "$custom_sysroot/usr/include/ecore-imf-1",
    "$custom_sysroot/usr/include/ecore-imf-evas-1",
    "$custom_sysroot/usr/include/ecore-input-1",
    "$custom_sysroot/usr/include/ecore-wayland-1",
    "$custom_sysroot/usr/include/ecore-wl2-1",
    "$custom_sysroot/usr/include/efl-1",
    "$custom_sysroot/usr/include/eina-1",
    "$custom_sysroot/usr/include/eina-1/eina",
    "$custom_sysroot/usr/include/emile-1",
    "$custom_sysroot/usr/include/eo-1",
    "$custom_sysroot/usr/include/evas-1",
    "$custom_sysroot/usr/include/system",
    "$custom_sysroot/usr/include/wayland-extension"
  ]
  
  lib_dirs = [ root_out_dir, "$custom_sysroot/usr/lib" ]

  cflags_cc = [
    "-Wno-newline-eof",
    "-Wno-macro-redefined",
  ]

  libs = [
    "base-utils-i18n",
    "capi-appfw-application",
    "capi-system-info",
    "capi-system-system-settings",
    "dlog",
    "ecore",
    "ecore_imf",
    "ecore_input",
    "eina",
    "EGL",
    "evas",
    "flutter_engine",
    "GLESv2",
    "tbm",
    "tdm-client",
    "wayland-client",
  ]

  if (tizen_sdk_4) {
    sources += [ "tizen_renderer_ecore_wl.cc" ]
    libs += [ "ecore_wayland", "wayland-egl" ]
    defines = [ "FLUTTER_TIZEN_4" ]
  } else {
    sources += [ "tizen_renderer_ecore_wl2.cc" ]
    libs += [ "ecore_wl2" ]
  }
}
